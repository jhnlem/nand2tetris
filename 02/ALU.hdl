// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    And16(a=x , b=false , out=zerox );
    Mux16(a=x , b=zerox , sel=zx , out=xz );

    Not16(in=xz , out=notx );
    Mux16(a=xz , b=notx , sel=nx , out=xn );

    And16(a=y , b=false , out=zeroy );
    Mux16(a=y , b=zeroy , sel=zy , out=yz );

    Not16(in=yz , out=noty );
    Mux16(a=yz , b=noty , sel=ny , out=yn );

    Add16(a = xn, b = yn, out = addxy);
    And16(a=xn , b=yn , out=andxy );
    Mux16(a=andxy , b=addxy , sel=f , out=o1 );

    Not16(in=o1 , out=o2 );
    Mux16(a=o1 , b=o2 , sel=no , out=out , out[0..7]=o07 , out[8..15]=o815 , out[15]=o15);
    
    Or8Way(in=o07 , out=O8o07 );
    Or8Way(in=o815 , out=O8o815 );
    Or(a=O8o07 , b=O8o815 , out=Orout );
    Not(in=Orout , out=zr );
    And(a=o15 , b=true , out=ng );
}